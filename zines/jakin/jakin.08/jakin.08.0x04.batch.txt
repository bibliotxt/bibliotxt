                               ==Jakin ezine==

                  <0x08>garren alea, <0x04>garren artikulua

                  
|=---------------------=[ DOS Batch programazioa ]=----------------------=|
|=-----------------------------------------------------------------------=|
|=----------------------------=[ NHTaldea ]=-----------------------------=|
|=--------------------=[ nhtaldea yahoo puntu com ]=---------------------=|


	
   
     0.- Hitzaurrea	
     1.- Kaixo Mundua
     2.- Aldagaiak
     3.- Baldintzak eta jauziak
     4.- Menuak
     5.- Begiztak
     6.- Kanpo fitxategiei deika
     7.- Teknikak
     8.- DOS agineduen laburpena
     9.- Erreferentziak


 0.- Hitzaurrea
 --------------
 Bash eta besteleko Unix-like sistemetan bezala, Ms-DOS sisteman ere 
script fitxategiak sor daitezke, agindutaz betetako .BAT atzizkidun fitxategiak
sortuz. .BAT fitxategia batean MSDOS aginduak sar daitezke eta funtzio
konplexuak lortzeko programazio tresna oinarrizko batzuk erabili daitezke:
baldintzak, begiztak, aldagaiak, e.a....

 Betidanik batch fitxategien programazioa benetazko artisautza dirudi,
izan ere dokumentazio eskasa dago honi buruz eta lengoaiaren murrizpenak
direla eta zenbait funtzionamendu lortzeko hack bereziak bilatu behar dira.

 Batch fitxategiak programatzeko beraz, DOS aginduak eta programazio
tresnak eta teknika izkutu horiek ezagutu behar dira. Testu honetan laburpen
egokia topatuko duzula espero dut.



 1.- Kaixo Mundua
 -----------------

Batch fitxategi hauek sortzeko testu editore bat erabili daiteke eta DOS aginduak
sartu behar dira. Hasteko iruzkinak eta pantailaratzeak nola egin ikusiko dugu,
Kaixo Mundua adibide klasikoarekin.

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
rem Hau iruzkin bat da
:: baita hau ere

echo Kaixo Mundua
--------8<--------------------------------------------------------------


Beste adibide bat, echoa kentzean eleganteagoa geratzen da.

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: echoa kentzen...
@echo off

:: echo off bitartez scriptaren aginduak ez dira isladatuko
:: Beste modu bat hori lortzeko, aginduen aitzinean @ bat sartu

echo Kaixo Mundua

:: Lerro hutsak:
echo.
echo.

:: echoa berrezarri
@echo on

echo Hau ikusten da

@echo Orain testua soilik
--------8<--------------------------------------------------------------


Adibide honetan direktorio batera joan eta fitxategiak ezabatzen dira.
pause erabiliz script-a geldiarazten da.

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: fitxategiak ezabatzeko scripta

@echo off
echo Gozen fitxategiak ezabatzera

cd c:\temp 

echo Adi! fitxategiak ezbatzera noa.
echo Ctrl-C bertan behera uzteko, edozein tekla jarraitzeko

pause

:: *.tmp fitxategi guztiak ezabatzen dira
del *.tmp

echo OK, fitxategiak ezabatu dira
--------8<--------------------------------------------------------------

Beste modu bat ezabatzerakoan zerbait egin den ala ez jakiteko && eta || erabiltzea
da. Agindua ongi funtzionatu duenean && ondoren dagoena egikarituko da. Gaizki
ateratzen denean || ondoren dagoena egikarituko da.

 del *.tmp && echo OK, ezbatu dira
 
 del *.tmp || echo Ez dira ezabatu!



 2.- Aldagaiak
 -------------

Aldagaiak izendatzeko set egindua erabiltzen da eta batch fitxategiaren parametroak
jasotzeko %1, %2,... erabiltzen da %9 arte. %0 batch fitxategiaren izena da, eta bederatzi
baino parametro gehiago behar izanez gero, shift agindua erabili daiteke.

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
@echo off

::
:: ALDAGAIAK eta PARAMETROAK
::

set ADIBIDEA="Mezua"
set izena=nhtaldea
set Zenbakia=666

echo Aldagaien balioa: %ADIBIDEA% , %izena% eta %Zenbakia%
echo Case sensitive al da? %zenbakia%  badirudi ezetz...

:: Aldagai bereziak: %0, %1, %2,... %9
echo %%0 zer da?  %0 fitxategi honen izena da
echo %1, %2, e.a... pasatzedn dizkiogun parametroak, %9 arte

:: %9 parametro baino gehiago behar ditugu? shift erabili!!!
set ald1=%1
set ald2=%2
set ald3=%3
set ald4=%4
set ald5=%5
set ald6=%6
set ald7=%7
set ald8=%8
set ald9=%9

:: Beste bi parametro sartzeko...
shift
set ald10=%9
shift
set ald11=%9

echo Aldagaien balioak
echo %ald1%, %ald2%, %ald3%, %ald4%, %ald5%
echo %ald6%, %ald7%, %ald8%, %ald9%
echo Parametro extrak: %ald10% eta %ald11%
--------8<--------------------------------------------------------------

Aldagai berezi batzuk. hauek prompt aginduarekin batera ikus daitezke,
baina ez da erabilpen bakarra. Sinbolo bereziak pantailaratzeko modu
bat da:

  $$     : $ dolarraren sinboloa
  $q     : berdin = sinboloa
  $g     : > handiago-baino sinboloa
  $l      < txikiago-baino sinboloa
  $b      | pipe edo hodiaren sinboloa
  $_      lerro berria
  $e      ASCII escape-ren kodea (27garrena)
  $h      backspace
  $t      momentuko ordua
  $d      momentuko data
  $p      path eta dispositiboa
  $v      DOS bertsio zenbakia
  $n      Momentuko dispositiboa (C:,A:,D:,...)


 3.- Baldintzak eta jauziak
 ------------------------

if egitura murriztu bat daukagu eta horretaz gain goto eta etiketak. Azken hauekin
kontrol egitura konplexuak sor daitezke: begiztak, azpierrutinak, e.a...

3 motako if aginduak sortu daitezke:
 * if exists :fitxategiak existitzen diren ala ez galdetzeko
    ezezkoa ere sor daiteke  if not exists...
 * if %aldagaia% == balioa : motako konparaketak
    bere ezezkoa   if not %1 == 3
 * if errorlevel 4 aurreko aginduaren irteera kodea konparatzen da
    eta ezezkoa  if not errorlevel 4
    
Goazen adibide xinple batzuekin
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: goto jauziak
@echo off

goto bukaera

echo Hau ez da inoiz aterako

:bukaera
echo Bukaera da, bukaera aurrez sentitzen da...
--------8<--------------------------------------------------------------


if exists estiloko baldintzekin fitxategi bat existitzen den ala ez begiratzen
da.
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: if egiturak: exist

@echo off

:: (1) if exists :fitxategiak existitzen diren ala ez galdetzeko
if exist b3.bat echo existitzen da

:: baliokidea bi lerrotan
if exist '*.bat' \
echo bat fitxategiak existitzen dira

:: if not exist
if not exist '*.exe' echo EXE fitxategirik ez dago

:: if else gotoen bitartez
if exist b2.bat goto egia
 echo Ez da existitzen
 goto endif
:egia
 echo b2.bat existitzen da
:endif


:: if elseif else egitura
if exist b0.bat goto egia0
if exist b1.bat goto egia1
if exist b2.bat goto egia2
if exist b3.bat goto egia3
 echo Bat ere ez da betetzen
:egia3
 echo b3.bat existitzen da
 goto endif
:egia2
 echo b2.bat existitzen da
 goto endif
:egia1
 echo b1.bat existitzen da
 goto endif
:egia0
 echo b0.bat existitzen da
 goto endif
:endif

--------8<--------------------------------------------------------------


if baldintzak balioak alderatzeko:

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: if egiturak == konparaketak

@echo off

set zenbakia=666
set izena="Jason"
set izena2=Jason

:: argumentuak aztertzen dira
if "%1"=="" echo Ez da parametrorik pasatu && goto bukaera

echo Jasotako parametroak %1 %2

:: (2) if bat balioen konparaketarako
if %1==%izena% echo izenaren berdina da

if %1==%izena2% echo izena2ren berdina da

if %1==%zenbakia% echo zenbakiaren berdina da

:: ADI! Balio hutsek errorerik ez emateko puntuak erabiltzen dira
if %2.==.%zenbakia% echo zenbakiaren berdina da

if not %1==%izena% echo izenaren desberdina da

if not %1==%izena2% echo izena2ren desberdina da

if not %1==%zenbakia% echo zenbakiaren desberdina da

:: AND bat egiteko, if batzuk kateatu daitezke!
if %1==%izena2% if %2.==.%zenbakia% echo Biak betetzen dira

if not %1==%izena2% if not %2.==.%zenbakia% echo Ez dira betetzen


:bukaera


--------8<--------------------------------------------------------------



if errorlevel bitartez aurreko aginduaren irteera aztertzen da:

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: if egiturak errorlevel

@echo off

:: Programa guztiek irteera bezala zenbaki bat ematen dute.
:: irteera hori errorlevel bitartez aztertu daiteke.
:: Adibidez:
:: if errorlevel 5 
:: errorlevel balioa 5 edo handiagoa denean TRUE izango da

:: argumentuak aztertzen dira
if "%1"=="" echo Ez da parametrorik pasatu && goto bukaera

dir %1 

:: Honekin irteera balioa zein den ikus daiteke.
echo %errorlevel%

:: Dena ongi dagoenean balioa 0 izan beharko

if errorlevel 1 echo Errorea gertatu da

if not errorlevel 1 echo Errorerik ez dago
--------8<--------------------------------------------------------------


 4.- Menuak
 ------------

Menuak sortzeko choice agindua erabili behar da menuaren aukeraz zehazteko. Sartutako
balioa aztertzeko errorlevel balioa begiratzen da.
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: choice menuak sortzeko tresna

@echo off


:: Lehenbizi menua aurkeztu behar da
:: Pantaila garbitu...
cls

echo.
echo Egin ezazu aukera
echo.
echo  1. Su Ta Gar
echo  2. Soziedad Alkoholika
echo  3. Latzen
echo  e. Bat ere ez
echo.
CHOICE  "Zure aukera" /c:123e /N

:: Sartutakoa kontrolatzeko errorlevel erabili!
if errorlevel 4 echo Bat ere ez 
if errorlevel 3 echo Latzen handienak ziren
if errorlevel 2 echo S.A. egungo nausiak
if errorlevel 1 echo SuTa hori bai kaña
--------8<--------------------------------------------------------------

Choiceren aukerak

CHOICE /C:aAbc /S        /S case sensitive izateko
CHOICE /C:abc /T:a.10   /T:a.10 hamar segundu pasa ostean a aukeratu

Modu politagoa menua antolatzeko
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: choice menuak sortzeko tresna

@echo off


:berriz

:: Lehenbizi menua aurkeztu behar da
:: Pantaila garbitu...
cls

echo.
echo Egin ezazu aukera
echo.
echo  1. Su Ta Gar
echo  2. Soziedad Alkoholika
echo  3. Latzen
echo  e. Bat ere ez
echo.
CHOICE  "Zure aukera" /c:123e /N

:: Sartutakoa kontrolatzeko errorlevel erabili!
if errorlevel 4 goto suta
if errorlevel 3 goto sa
if errorlevel 2 echo latzen
if errorlevel 1 echo berriz


:suta
echo aukeratu duzu
goto bukaera

:sa
echo S.A. aukeratu duzu
goto bukaera

:latzen
echo Latzen aukeratu duzu
goto bukaera


:bukaera
echo Bukatu da akabo
--------8<--------------------------------------------------------------

DOS modernoetan choice hau desagertu dela dirudi...


 5.- Begiztak
 -------------

Begiztak sortzeko gotoak erabili daitezke ala for egitura.
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: for begiztak

@echo off

:: For honek bat bi hiru lau pantailaratzen ditu
:: kontuz, aldagaia hizki bakarrekoa izan behar du
for %%z  in (bat, bi, hiru, lau) do echo %%z

:: honek efektu berdintsua dauka
for %%z  in (bat bi hiru lau) do echo %%z

:: Pasatutako parametroak pantailaratzen dira
for %%p in (%0 %1 %2 %2 %3 %4 %5 %6 %7 %8 %9) do echo Parametroak %%p
--------8<--------------------------------------------------------------

FOR egiturak aukera gehiago dauzka. Aukera hauek agian ez dute DOS
zaharretan funtzionatzen.
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: for begiztak, aginduak erabiliz

@echo off

:: FOR /L  bat nondik norakoa eta pausu modua ezarriz
:: Kasu honetan 10etik 1era
for /L %%i in (10,-1,1) do echo %%i

for /L %%i in (0,+1,10) do echo %%i

:: FOR  fitxategia patroiekin begizta egiteko
:: BAT fitxategi guztiak begiratzen ditu
for %%f  in (*.bat) do echo .bat fitxategia: %%f 
::for %%f  in (*.bat) do echo FICHERO %%f && type %%f && pause

:: Agindu baten irteeratik FOR bat egin
for /F %%i in ('dir /b *.bat') do echo %%i
--------8<--------------------------------------------------------------


 6.- Kanpo fitxategiei deika
 --------------------------

BAT fitxategitik edozein programa egikaritu daiteke agindua jarriz. Beste batch
fitxategi bat egikaritzeko call erabiltzen da.
Adibide honetan batch fitxategi batetik beste bat erabiliko dugu. Beste hori liburutegi
antzeko bat osatzen du, azpierrutinekin.
------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: kanpo fitxategiei deiak egiten

@echo off

call lib.bat %1 %2
--------8<--------------------------------------------------------------


Hona hemen nolabaiteko batch "liburutegia":

------------8<----------------------------------------------------------
:: jakin #08 - batch fitxategiak - nhtaldea
:: azpierrutinaz betetako liburutegia

@echo off

:: Liburutegi honetan fitxategiekin gauza ezberdinak egin
:: daitezke: dir, type, attrib eta backup bat Horretarako liburutegi
:: hau call eta parametroekin egikaritu behar da.

if "%1"==""  goto faltadira
if "%2"==""  goto faltadira
if not exist %2 goto ezexist

:: Sartutako aginduaren arabera gauza bat ala bestea egikarituko da,
:: nolabait azpierrutinen simulazioa lortuz.
if %1==dir goto dir
if %1==type goto type
if %1==attrib goto attrib
if %1==backup goto backup

:dir
echo DIR bat egin %2 fitxategiarekin
dir %2
goto bukaera

:type
echo TYPE bat egin %2 fitxategiarekin
type %2
goto bukaera

:attrib
echo ATTRIB bat egin %2 fitxategiarekin
attrib %2
goto bukaera

:backup
echo BACKUP bat egin %2 fitxategiarekin
copy %2 %2.bak
goto bukaera

:ezexist
echo %2 fitxategirik ez dago
goto bukaera

:faltadira
echo Parametroak falta dira

:bukaera


--------8<--------------------------------------------------------------

 7.- Teknikak
 ------------

Sistema eta shellaren izaeraz baliatuta batch fitxategiekin gauza aunitz egin daitezke,
edukiaren bideraketa < edo > edo >> erabiliz, eta prozesuen komunikazioa | bitartez.

 < honen bitartez fitxategi edo dispositibo baten edukia agindu bati pasatzen zaio 
 > honekin aginduaren irteera fitxategi edo dispositibo bati pasatzen zaio
 >> aurrekoaren berdina, baina gainean idatzi ordez bukaeran gehitzen du
| agindu baten irteera beste agindu baten sarrera izateko

Adibidez, 'y' pasatu del aginduari:

 echo y | del *.* 


Bestalde, unixen sarrera/irteera/errore estandarrendako deskriptoreak dauden 
moduan, batch ingurunean oinarrizko dispositibo hauek dauzkagu:
- con   kontsola, bi zentzutan: pantaila eta teklatua
- comn serie portua: com1, com2, com3, com4
- lptn   portu paraleloa: lpt1, lpt2, lpt3, lpt4
- err   errore estandarra
- aux  auxiliarra, com1
- nul   /dev/null en baliokidea.
- prn  inprimagailua
- clock$ denbora errealeko erlojua

Bideratze adibidea

 @echo off
 
 echo Bilatu %1 %2 fitxategian
 
 find %1 < %2 >nul
 if not errorlevel 1 echo %2

Irteera izkutzatzko modu xinplea:

 copy test.bat c:\msdos\ > null


ANSI moduak
------------------

Bash scriptei buruzko artikuluan (Jakin #7) honi buruzko zerbait esaten
zen. ANSI estandarra sistema ezberdinetan topa daiteke, eta DOS sisteman
ere kontsola ANSI onartzeko aukera ezarri daiteke. Horretarako config.sys
fitxategian holako lerroa sartu behar da (kasu honetan win98 batean): 

 device=c:\windows\command\ansi.sys

Hortik aurrera ANSI kodeak erabili daitezke escape karakterea sartuz. Karaktere
hori sartzeko ESC edo alt-27 sakatu beharko da (Bloq Num aktibatuta).

Orokorrean ANSI kodeak horrela adierazi behar dira:

ESC[agindua

Adibidez:

prompt Windozer ESC[1;32;40m

+Kurtsorea mugitzeko:

ESC[x;yH   (x lerroa, y zutabea)
ESC[#A  gora, ESC[#B  behera, ESC[#C  aurrera,ESC[#D  atzera,...

ESC[2J pantaila garbitu
ESC[K lerroa garbitu...

+Koloreak
 Orokorrean ESC[modua;aurrekolorea;atzekoloream
 Moduak: 
0 ezarpenak kendu
1 letra lodia
4 azpimarra
5 dirdira egin
7 alderantzizkoa
8 izkutatu

aurrekol.      atzekol.
----------------    ------------      
      30             40       beltza
      31             41       gorria
      32             42       berdea
      33             43       horia
      34             44       urdina
      35             45       magenta
      36             46       cyan
      37             47       txuria

Hona hemen adibidetxo bat:
------------8<----------------------------------------------------------
@echo off

echo ANSI kodeen adibidea
:: escape sartzeko alt+27

echo ESC[0;30;40m Test bat da: 30;40
echo ESC[0;31;40m Test bat da: 31;40
echo ESC[0;32;40m Test bat da: 32;40
echo ESC[0;33;40m Test bat da: 33;40
echo ESC[0;34;40m Test bat da: 34;40
echo ESC[0;35;40m Test bat da: 35;40
echo ESC[0;36;40m Test bat da: 36;40
echo ESC[0;37;40m Test bat da: 37;40

::Atzeko kolorea aldatuz:
echo ESC[5;30;40m Test bat da: 30;40
echo ESC[5;31;41m Test bat da: 31;41
echo ESC[5;32;42m Test bat da: 32;42
echo ESC[5;33;43m Test bat da: 33;43
echo ESC[5;34;44m Test bat da: 34;44
echo ESC[5;35;45m Test bat da: 35;45
echo ESC[5;36;46m Test bat da: 36;46
echo ESC[5;37;47m Test bat da: 37;47

:: kolore arruntak ezarri
echo ESC[0m dena normal utzi

--------8<--------------------------------------------------------------

ANSI-ri buruz beste gauza batzuk esan daitezke, lehergailu ospetsuak
adibidez, baina hori beste baterako utzi beharko. Gainera pixkat old-school
gaia da.

Teknika askoz gehiago paper ikusgarri honetan ikus daitezke. Argi geratzen da
zer nolako trikimailuak egin behar diren operazio errezak burutzeko.

Dirk Van Deun, Collection of Batch Techniques: 
http://student.vub.ac.be/~dvandeun/batcoll.all


 8.- DOS agineduen laburpena
 ----------------------------

Batch fitxategitik DOS aginduak erabiltzeko aukera daukagunez, agindu ezagunenak
zeintzuk diren jakitea komeni da. Bakoitzatik laguntza jasotzeko 
 
  help agindua
  agindua /?

egin beharko da.

Jarraian DOS aginduak (guztiak?) agertzen dira. Gehienak ezagunak dira
baina agia bat edo beste berria egiten zaizu.

DOS aginduen zerrenda:

ASSOC     Fitxategien estentsioen erlazioak erakutsi ala ezartzeko
AT          Agindu eta programen egikaritzea une baterako programatzen du
ATTRIB    Fitxategien baimenak erakutsi ala ezartzeko
BREAK     Ctrl+C-ren konprobazioa ezarri ala ezabatzeko
CALCS     Fitxategien ACLak erakutsi ala ezartzeko
CALL       Batch fitxategi bat egikaritzeko.
CD          Direktorioa aldatu ala momentuko direktorioa erakusten du
CHCP      Kode orri zenbakia erakutsi ala ezartzen du
CHDIR     Momentuko direktorioa erakutsi ala aldatzeko
CHKDSK   Diskoa konprobatu eta egoerari buruz informazioa ematen du.
CHKNTFS  Hasieraketan disko konprobaketa egiteko aukera ezartzeko
CLS        Pantaila garbitzen du, clear-en baliokidea
CMD       Shell bat sortzen du, /bin/bash-en baliokidea
COLOR    Kontsolaren koloreak ezartzeako
COMP     Bi edo fitxategi gehiagoren edukina konparatzen ditu.
COMPACT  NTFS partizioen fitxategi konpresio egoera erakutsi ala aldatzeko
CONVERT  FAT partizioak NTFSra bihurtzeko
COPY    Fitxategiak kopiatzeko
DATE    Eguna erakutsi eta ezartzeko
DEL      Fitxategi bat edo gehiago ezabatzeko
DIR       Direktorioen edukina erakusten du
DISKCOMP Bi disketeen edukina konparatzeko
DISKCOPY Disketeak batetik bestera kopiatzeko.
DOSKEY   Sartutako aginduak berreskuratu, makroak, azken finean aginduak 
              idazteko erreztasunak
ECHO        Mezuak idatzi eta echo-a ezarri ala kentzeko
ENDLOCAL Inguruneko aldagai bilaketa amaitzeko batch fitxategian
ERASE    Fitxategi bat ala gehiago ezabatzeko
EXIT      Shell-atik irtetzeko
FC         Bi ala fitxategi gehiago konparatu eta ezberdintasunak erakutsi, diff-en antzekoa
FIND      Testu kate bat fitxategietan bilatzeko
FINDSTR  Testu kateak fitxategietan bilatzeko
FOR        For begiztak sortzeko
FORMAT  Disko edo partizioei formatoa emateko
FTYPE    Fitxategi elkartetan fitxategi motak erakutsi ala aldatzeko
GOTO     Jauziak sortzeko
GRAFTABL Windows-en karaktere jokuaren luzapena erakusteko ahalbidetzen du modu grafikoan
HELP      Laguntza ematen du
IF          Baldintzak sortzeko
LABEL     Partizio baten izena sortu, aldatu ala ezabatzeko
MD        Direktorioa sortzeko
MKDIR    Direktorioa sortzeko
MODE    Sistema dispositiboa konfiguratzeko
MORE    Pantailaratutako informazioa saltoka erakusteko, linux-en more
MOVE    Fitxategi bat ala gehiago lekuz mugitzeko
PATH     Programen gunea erakutsi ala ezartzeko
PAUSE    Batch baten egikaritzea eteteko
POPD     PUSHD-rekin gordetako direktoriora goaz
PRINT    Testu fitxategia inprimatzeko
PROMPT   Prompt-a aldatzeko
PUSHD    Momentuko direktoria gorde eta gero aldatzen du
RD        Direktorioak ezabatzen ditu
RECOVER  Apurtutako disko baten informazioa berreskuratzeko
REM      Iruzkinak sartzeko
REN      Fitxategi bat ala gehiagoren izena aldatu
RENAME   Fitxategi bat ala gehiagoren izena aldatu
REPLACE  Fitxategiak berraldatzeko
RMDIR    Direktorioak ezabatzeko
SET      Aldagaiak erakutsi, ezarri ala ezabatzeko
SETLOCAL Inicia la localización de cambios del entorno en un archivo por lotes.
SHIFT    Batch fitxategien argumentuak zerrenda luzatzeko
SORT     Edukinak ordenean jartzeko
START    Lehio berri batean shell bat sortzeko,
SUBST    Atzipen gune bat unitate hizki bati lotu
TIME     Ordua erakutsi ala ezartzen du
TITLE    Lehioaren izenburua ezartzen du
TREE     Unitate edo direktorio baten zuhaitz egitura grafikoki erakusten du
TYPE     Testu fitxategien edukina erakusten du
VER      Windows edo DOSen bertsio zenbakia erakusten du
VERIFY   Windows-i fitxategiak modu egokian idazten direla konprobatzeko adierazten dio 
VOL      Disko edo partizioaren serie zbkia eta izena erakusteko
XCOPY    Fitxategiak eta direktorio egiturak kopiatzeko


 9.- Erreferentziak
 -----------------

Hona hemen gai honi buruzko erreferentzia interesgarri batzuk, gehiago daude.
Batzuk batch birusak sortzeko dira, maila bajuko birusak kontsideratzen dira
baina batch teknikak ikasteko behintzat balio dute.

* HELP agindua
* Batch Guide,  by Terry Newton [en]
* Collection of Batch Techniques, Dirk Van Deu [en]
  (http://student.vub.ac.be/~dvandeun/batcoll.all)
* Batch File Programming by Ankit Fadia [en]

Birusei buruz:
* RareGaZz #010: virus batch I [es]
* RareGaZz #015: virus batch II [es]


Gai hau zaharkituta dagoela pentsa daiteke, baina DOS, cmd edo dena delakoa dagoen bitartean
erabilgarria izanen delakoan nago.
Agurrak
                        -NHTaldea-

<E0F>
